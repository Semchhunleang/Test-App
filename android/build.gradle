buildscript {
    ext.kotlin_version = '1.9.10'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.android.tools.build:gradle:8.0.2'
        // classpath 'com.google.gms:google-services:4.3.15'
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
    }
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    project.evaluationDependsOn(':app')

    // --- Force all modules/plugins to use Java 17 / Kotlin 17 safely ---
    project.plugins.withType(com.android.build.gradle.BasePlugin) {
        project.extensions.configure(com.android.build.gradle.BaseExtension) { androidExt ->
            androidExt.compileOptions {
                sourceCompatibility JavaVersion.VERSION_17
                targetCompatibility JavaVersion.VERSION_17
            }
        }
    }

    project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "17"
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
